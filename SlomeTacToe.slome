DO ALLOC 10000;
MAKE ARRAY board 0,0,0,0,0,0,0,0,0;
MAKE NUMBER toMove 1;
DO "turn" :board:;
MAKE STRING winner "";
SKIP TO "else" WHEN @toMove==2@;
    DO "toIcon" :2: TO winner;
    SKIP TO "endif";
SKIP LOCATION else;
    DO "toIcon" :1: TO winner;
SKIP LOCATION endif;
DO SAY "\n",winner,"\sWon!\n";
DO PAUSE;
END 0;
DEFINE FUNC VOID turn :b:;
    DO "showBoard" :b:;
    MAKE STRING icon "";
    MAKE NUMBER toChange 0;
    DO "toIcon" :toMove: TO icon;
    SKIP TO "else" WHEN @toMove==2@;
        DO SET toChange toMove;
        DO SET toMove 2;
        SKIP TO "endif";
    SKIP LOCATION else;
        DO SET toChange toMove;
        DO SET toMove 1;
    SKIP LOCATION endif;
    DO SAY "Make\syour\smove!(",icon,")\n";
    MAKE NUMBER index 0;
    DO LISTEN TO index;
    SKIP TO "lose" WHEN @index>9@;
    SKIP TO "lose" WHEN @index<1@;
    SKIP TO "lose" WHEN @b#:index-1:>0@;
    DO SET b#:index-1: toChange;
    MAKE NUMBER hasWon 0;
    DO "checkWin" :b,toChange: TO hasWon;
    SKIP TO "endif" WHEN @hasWon==0@;
        DO RETURN;
    SKIP LOCATION endif;
    DO "turn" :b:;
    DO RETURN;
    SKIP LOCATION lose;
        DO SAY "Not\sa\svalid\smove!\sYou\slose!\n";
        DO PAUSE;
        END 0;
ENDFUNC;
DEFINE FUNC VOID showBoard :b:;
    MAKE NUMBER i 0;
    MAKE NUMBER j 0;
    MAKE STRING icon "";
    SKIP LOCATION loopI;
        SKIP LOCATION loopJ;
            DO "toIcon" :b#:i*3+j:: TO icon;
            DO SAY icon,"\s";
            DO SET j :j+1:;
        SKIP TO "loopJ" UNLESS @j>2@;
        DO SET j 0;
        DO SET i :i+1:;
        DO SAY "\n";
    SKIP TO "loopI" UNLESS @i>2@;
    DO RETURN;
ENDFUNC;
DEFINE FUNC STRING toIcon :v:;
    SKIP TO "notO" UNLESS @v==1@;
        DO RETURN "O";
    SKIP LOCATION notO;
    SKIP TO "nothing" UNLESS @v==2@;
        DO RETURN "X";
    SKIP LOCATION nothing;
    DO RETURN "#";
ENDFUNC;
DEFINE FUNC NUMBER checkWin :b,whoToPlay:;
    MAKE NUMBER i -1;
    SKIP LOCATION loopI;
        DO SET i :i+1:;
        SKIP TO "checkHorizontal" UNLESS @b#:i:==whoToPlay@;
        SKIP TO "checkHorizontal" UNLESS @b#:i+3:==whoToPlay@;
        SKIP TO "checkHorizontal" UNLESS @b#:i+6:==whoToPlay@;
        DO RETURN 1;
        SKIP LOCATION checkHorizontal;
        SKIP TO "endCheck" UNLESS @b#:i*3:==whoToPlay@;
        SKIP TO "endCheck" UNLESS @b#:i*3+1:==whoToPlay@;
        SKIP TO "endCheck" UNLESS @b#:i*3+2:==whoToPlay@;
        DO RETURN 1;
        SKIP LOCATION endCheck;
    SKIP TO "loopI" UNLESS @i>1@;
    SKIP TO "checkDiaginalTwo" UNLESS @b#0==whoToPlay@;
    SKIP TO "checkDiaginalTwo" UNLESS @b#4==whoToPlay@;
    SKIP TO "checkDiaginalTwo" UNLESS @b#8==whoToPlay@;
    DO RETURN 1;
    SKIP LOCATION checkDiaginalTwo;
    SKIP TO "endif" UNLESS @b#3==whoToPlay@;
    SKIP TO "endif" UNLESS @b#4==whoToPlay@;
    SKIP TO "endif" UNLESS @b#6:=whoToPlay@;
    DO RETURN 1;
    SKIP LOCATION endif;
    DO RETURN 0;
ENDFUNC;